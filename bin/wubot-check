#!/usr/local/bin/perl -w
use strict;
use warnings;

our $VERSION = '0.5.0'; # VERSION

use AnyEvent;
use FindBin;
use Log::Log4perl;
use Sys::Hostname;
use YAML::XS;

my $wubotrc_data;
BEGIN {
    $wubotrc_data = YAML::XS::LoadFile( "$ENV{HOME}/.wubotrc" );
}
use lib "$wubotrc_data->{wubot_project}/lib";

use App::Wubot::LocalMessageStore;
use App::Wubot::Logger;
use App::Wubot::SQLite;
use App::Wubot::Check;
use App::Wubot::WubotX;
use App::Wubot::Wubotrc;

my $wubotrc = App::Wubot::Wubotrc->new();

# load plugin library paths
my $wubotx = App::Wubot::WubotX->new();
$wubotx->get_plugins();

my $logger = Log::Log4perl::get_logger( 'default' );

my ( $plugin, $key ) = @ARGV;
unless ( $plugin && $key ) {
    die "ERROR: you must specify both a plugin and a key name!"
}

$logger->warn( "PLUGIN: $plugin" );
$logger->warn( "KEY:    $key" );

my $config_directory = $wubotrc->get_config( 'config_home' );
my $glob = "$config_directory/plugins/$plugin/$key.yaml*";
my ( $config_file ) = glob( $glob );

unless ( $config_file ) {
    die "ERROR: config file not found: $glob";
}

unless ( -r $config_file ) {
    $logger->logdie( "ERROR: config file not found: $config_file" );
}
my $config = YAML::XS::LoadFile( $config_file );
$config->{nofork} = 1;
$logger->info( "CONFIG: ", YAML::XS::Dump $config );

my $cache_directory = $wubotrc->get_config( 'cache_home' );
my $cache_file = "$cache_directory/$plugin-$key.yaml";
if ( -r $cache_file ) {
    $logger->info( "Found cache file: $cache_file" );
}

my $messenger = App::Wubot::LocalMessageStore->new( { novacuum => 1 } );

my $check_h = { key           => "$plugin-$key",
                class         => "App::Wubot::Plugin::$plugin",
                reactor_queue => $messenger,
                cache_file    => $cache_file,
            };

my $check = App::Wubot::Check->new( $check_h );

my $j = AnyEvent->condvar;

print YAML::XS::Dump { config => $config };

$logger->info( "Initializing" );
my $init_results = $check->init( $config );

$logger->info( "Checking" );
eval {                          # try
    my $results = $check->check( $config );

    print "RESULTS\n";
    print YAML::XS::Dump { results => $results };
    1;
} or do {                       # catch
    print "ERROR RUNNING CHECK: $@\n";
    exit;
};


$j->wait;

__END__

=head1 NAME

 wubot-check - perform a single check of an instance of a plugin

=head1 VERSION

version 0.5.0

=head1 SYNOPSIS

  wubot-check Plugin id

  # example
  wubot-check RSS slashdot

=head1 DESCRIPTION

This script makes it easy to perform a single check of an instance of
a monitor.

See also: L<App::Wubot::Guide::Debugging>
